#!/usr/bin/env perl
# xtetsuji 2014/06/17
# need Cocoa::DictionaryServices
#   https://github.com/akiym/Cocoa-DictionaryServices

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage qw(pod2usage);
use Term::ANSIColor;

BEGIN {
    local $@;
    eval {
        require Cocoa::DictionaryServices;
        import  Cocoa::DictionaryServices qw(lookup available_dictionaries);
    };
    if ( $@ ) {
        print "Install Cocoa::DictionaryServices form GitHub\n";
        print "  you enter following command.";
        print "  cpanm git\@github.com:akiym/Cocoa-DictionaryServices.git\n";
        exit;
    }
}

my $p = Getopt::Long::Parser->new(
    config => [qw(posix_default no_ignore_case auto_help)]
);
$p->getoptions(
    'version!'            => \my $version,
    'uasge|help'          => \my $help,
    'l|list-dictionaries' => \my $list_dictionaries,
);

if ( $list_dictionaries ) {
    for my $key (available_dictionaries()) {
        print "$key\n";
    }
    exit;
}

if ( $help ) {
    pod2usage(0);
}

my ($keyword, $dictionary) = @ARGV;

my $contents;

if ( !$keyword ) {
    pod2usage(1);
} elsif ( !$dictionary ) {
    $contents = lookup($keyword) . "\n";
} else {
    $contents = lookup($keyword, guess_dictionary($dictionary)) . "\n";
}

# decorated (no utf8, lookup takes utf-8 byte string)
$contents =~ s/(【.*?】)/ colored($1, "blue") /e;
$contents =~ s/(〈.*?〉)/ colored($1, "blue") /e;
$contents =~ s/(〖.*?〗)/ colored($1, "blue") /e;
$contents =~ s/;/;\n\n/g; # 一行で帰ってくるから分割したほうがいい？

print $contents;

sub guess_dictionary {
    my $name = shift;
    my @available_dictionaries = available_dictionaries();
    my %dictionaries = map { lc $_ => $_ } @available_dictionaries;
    for my $dictionary ( @available_dictionaries ) {
        my $alias = join '', map { substr($_, 0, 1)  } split /[ -]+/, $dictionary;
        $dictionaries{lc $alias} = $dictionary;
    }
    return $dictionaries{lc $name} || '';
}

exit;

__END__

=pod

=head1 NAME

 define - Mac OS X dictionary tool

=head1 SYNOPSIS

 define KEYWORD
 define KEYWORD DICTIONARY
 define [-l|--list-dictinaries]

=head1 DESCRIPTION

define is Mac OS X dictionary tool.

You can see Dictionary list by `define -l` command.

define command supports dictionary short name.
If you use "Japanese-English", then you can specify "je".

=head1 REQUIRES

L<Cocoa::DictionaryServices>,
L<https://github.com/akiym/Cocoa-DictionaryServices>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2014 by OGATA Tetsuji

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
